structure A = Absyn

%%
%term
    EOF 
  | ID of string
  | INT of int | STRING of string 
  | COMMA | COLON | SEMICOLON | LPAREN | RPAREN | LBRACK | RBRACK 
  | LBRACE | RBRACE | DOT 
  | PLUS | UMINUS | MINUS | TIMES | DIVIDE | EQ | NEQ | LT | LE | GT | GE
  | AND | OR | ASSIGN
  | ARRAY | IF | THEN | ELSE | WHILE | FOR | TO | DO | LET | IN | END | OF
  | BREAK | NIL
  | FUNCTION | VAR | TYPE 
  | LOWER_THAN_FUNCTION | LOWER_THAN_TYPE | LOWER_THAN_ELSE

%nonterm  program of A.exp |
  exp of A.exp | exps of (A.exp * A.pos) list | 
  decs of A.dec list | dec of A.dec | 
  oper of A.oper | 
  lval of A.var | lval' of A.var |
  vardec of A.dec |
  ty of A.ty | tyfield of A.field | tyfields of A.field list | 
  tydec of {name: A.symbol, ty: A.ty, pos: A.pos} | tydecs of {name: A.symbol, ty: A.ty, pos: A.pos} list | tydec_wrap of A.dec |
  fundecs of A.fundec list | fundec_wrap of A.dec |
  fundec of A.fundec | fundec_params of A.field list | fundec_type of {exp: A.exp, result: (A.symbol * pos) option} |
  callParams of A.exp list | recexp of (A.symbol * A.exp * A.pos) | recexps of (A.symbol * A.exp * A.pos) list
 
  
%nonassoc LOWER_THAN_FUNCTION LOWER_THAN_TYPE LOWER_THAN_ELSE
%nonassoc FUNCTION TYPE ELSE OF DO
%nonassoc EQ NEQ LT LE GT GE
%right SEMICOLON
%left ASSIGN
%left OR
%left AND
%left PLUS MINUS
%left TIMES DIVIDE
%left UMINUS



%pos int
%verbose
%start program
%eop EOF
%noshift EOF

%name Tiger

%keyword WHILE FOR TO BREAK LET IN END FUNCTION VAR TYPE ARRAY IF THEN ELSE 
	DO OF NIL

%prefer THEN ELSE LPAREN

%value ID ("bogus")
%value INT (1)
%value STRING ("")

%%

program:        exp                                       (exp)

	
exp:            NIL		                                    (A.NilExp)
exp:            INT                                       (A.IntExp(INT))

exp:            ID                                        (A.VarExp (A.SimpleVar(Symbol.symbol ID, IDleft)))
exp:            lval'                                     (A.VarExp lval')
exp:		        STRING					                          (A.StringExp (STRING, STRINGleft))

callParams:     exp                                       (exp::nil)
callParams:     exp COMMA callParams                      (exp :: callParams)
exp:            ID LPAREN callParams RPAREN               (A.CallExp{func=(Symbol.symbol ID), args=callParams, pos=IDleft})
exp:            ID LPAREN RPAREN                          (A.CallExp{func=(Symbol.symbol ID), args=nil, pos=IDleft})

lval:           ID                                        (A.SimpleVar(Symbol.symbol ID, IDleft))
lval:          lval'                                      (lval')
lval':          lval DOT ID                               (A.FieldVar(lval, Symbol.symbol ID, lvalleft))
lval':          ID LBRACK exp RBRACK                      (A.SubscriptVar((A.SimpleVar(Symbol.symbol ID, IDleft)), exp, IDleft))
lval':          lval' LBRACK exp RBRACK                   (A.SubscriptVar(lval', exp, lval'left))
exp:            lval ASSIGN exp                         (A.AssignExp {var=lval, exp=exp, pos=lvalleft})


exp:            LPAREN exps RPAREN                        (A.SeqExp exps)
exp:            LPAREN RPAREN                             (A.SeqExp nil)                             
exps:           exp                                       ((exp, expleft) :: nil)
exps:           exp SEMICOLON exps                        ((exp, expleft) :: exps)

exp:            ID LBRACK exp RBRACK OF exp               (A.ArrayExp {typ=(Symbol.symbol ID), size=exp1, init=exp2, pos=IDleft})

recexp:         ID EQ exp                                (((Symbol.symbol ID),exp,IDleft))
recexps:        recexp                                   (recexp::nil)
recexps:        recexp COMMA recexps                     (recexp::recexps)
exp:            ID NIL                         (A.RecordExp {fields=[], typ=(Symbol.symbol ID), pos=IDleft})
exp:		ID LBRACE RBRACE			 (A.RecordExp {fields=[], typ=(Symbol.symbol ID), pos=IDleft})
exp:            ID LBRACE recexps RBRACE                 (A.RecordExp {fields=recexps, typ=(Symbol.symbol ID), pos=IDleft})

exp:            LET decs IN exps END                      (A.LetExp {decs=decs, body=(A.SeqExp exps), pos=LETleft})   

decs:		        dec						                            (dec::nil)
decs:		        dec decs					                        (dec::decs)

dec:            vardec                                    (vardec)
dec:            fundec_wrap                               (fundec_wrap)
dec:            tydec_wrap                                (tydec_wrap)

vardec:         VAR ID ASSIGN exp                         (A.VarDec {name=(Symbol.symbol ID), escape=(ref false), typ=NONE, init=exp, pos=VARleft})
vardec:         VAR ID COLON ID ASSIGN exp                (A.VarDec {name=(Symbol.symbol ID1), escape=(ref false), typ=SOME(Symbol.symbol ID2, ID2left), init=exp, pos=VARleft})

fundec_wrap:		fundecs					                          (A.FunctionDec fundecs)
fundecs: 	      fundec fundecs				                    (fundec :: fundecs)
fundecs:	      fundec					%prec LOWER_THAN_FUNCTION (fundec :: nil)


fundec:         FUNCTION ID fundec_params fundec_type		  ({name=(Symbol.symbol ID), params=fundec_params, result=(#result fundec_type), body=(#exp fundec_type), pos=FUNCTIONleft})
fundec_params:  LPAREN tyfields RPAREN                    (tyfields)
fundec_params:  LPAREN RPAREN                             ([])
fundec_type:    EQ exp                                    ({exp=exp, result=NONE})
fundec_type:    COLON ID EQ exp                           ({exp=exp, result=SOME((Symbol.symbol ID), IDleft)})


tydec_wrap:     tydecs                                    (A.TypeDec tydecs)
tydecs:		      tydec tydecs				                      (tydec :: tydecs)
tydecs:		      tydec		 			     %prec LOWER_THAN_TYPE  (tydec :: nil)

tydec:		      TYPE ID EQ ty				                      ({name=(Symbol.symbol ID), ty=ty, pos=TYPEleft})

ty:		          ID					                              (A.NameTy (Symbol.symbol ID, IDleft))
ty: 		        ARRAY OF ID				                        (A.ArrayTy (Symbol.symbol ID, IDleft))
ty:		          LBRACE tyfields RBRACE			              (A.RecordTy (tyfields))


tyfield: 	      ID COLON ID				                        ({name=(Symbol.symbol ID1), escape=(ref false), typ=(Symbol.symbol ID2), pos=ID1left})
tyfields: 	    tyfield					                          (tyfield :: nil)
tyfields:	      tyfield COMMA tyfields	 		              (tyfield :: tyfields)

exp:            MINUS exp                    %prec UMINUS (A.OpExp {left=A.IntExp(0), oper=A.MinusOp, right=exp, pos=MINUSleft})
exp:            exp TIMES exp                             (A.OpExp {left=exp1, oper=A.TimesOp, right=exp2, pos=exp1left})
exp:            exp DIVIDE exp                            (A.OpExp {left=exp1, oper=A.DivideOp, right=exp2, pos=exp1left})
exp:            exp PLUS exp                              (A.OpExp {left=exp1, oper=A.PlusOp, right=exp2, pos=exp1left})
exp:            exp MINUS exp                             (A.OpExp {left=exp1, oper=A.MinusOp, right=exp2, pos=exp1left})
exp:            exp EQ exp                                (A.OpExp {left=exp1, oper=A.EqOp, right=exp2, pos=exp1left})
exp:            exp NEQ exp                               (A.OpExp {left=exp1, oper=A.NeqOp, right=exp2, pos=exp1left})
exp:            exp LT exp                                (A.OpExp {left=exp1, oper=A.LtOp, right=exp2, pos=exp1left})
exp:            exp LE exp                                (A.OpExp {left=exp1, oper=A.LeOp, right=exp2, pos=exp1left})
exp:            exp GT exp                                (A.OpExp {left=exp1, oper=A.GtOp, right=exp2, pos=exp1left})
exp:            exp GE exp                                (A.OpExp {left=exp1, oper=A.GeOp, right=exp2, pos=exp1left})

exp:            IF exp THEN exp ELSE exp                  (A.IfExp {test=exp1, then'=exp2, else'=(SOME exp3), pos=IFleft})
exp:            IF exp THEN exp    %prec LOWER_THAN_ELSE  (A.IfExp {test=exp1, then'=exp2, else'=NONE, pos=IFleft})

exp:            WHILE exp DO exp                          (A.WhileExp {test=exp1, body=exp2, pos=WHILEleft})
exp:            FOR ID ASSIGN exp TO exp DO exp           (A.ForExp {var=(Symbol.symbol ID), escape=(ref false), lo=exp1, hi=exp2, body=exp3, pos=FORleft})

exp:            ID ASSIGN exp                             (A.AssignExp {var=(A.SimpleVar(Symbol.symbol ID, IDleft)),exp=exp,pos=IDleft})

exp:            BREAK                                     (A.BreakExp BREAKleft)
